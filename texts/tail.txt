man:{[x]
  if[10<>type str:(),x;'`type];                                 / string?
  if[trim[str]~"--list";                                        / --list option
    m:{(1+max count each x)$'x}key .man.d;                      /   keys
    f:(system"c")[1] div count first m;                         /   fold
    1 raze(raze each f cut m),'"\n";                            /   list
    :""];
  / syscmds and cmd-line options case sensitive
  str:("j"$not first[x]in"\\-")lower/str;                       
  url:.man.ROOT,{$[count x;x;.man.HELP]}.man.d str;             / URL
  cmd:"mwlsv"!("open";"start";"xdg-open")where 1 1 3;           / OS command
  system(cmd first string .z.o)," ",url;                        / in browser
  url }
\

Usage examples:

man "$"               / operator glyph
man "enum extend"     / operator name
man "read0"           / keyword
man ".z"              / namespace
man "-b"              / command-line option
man "\\b"             / system command
man ""                / reference card

Special pages:

man "cmdline"         / command-line options
man "errors"
man "datatypes"
man "debug"
man "help"
man "interfaces"
man "internal"
man "iterators"
man "kb"              / Knowledge Base
man "syscmds"         / system commands
man "wp"              / White Papers

All arguments to man:

man "--list"